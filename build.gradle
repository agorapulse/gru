/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2022 Agorapulse.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.checkstyle'
    id 'org.kordamp.gradle.codenarc'
    id 'org.kordamp.gradle.coveralls'
    id 'io.github.gradle-nexus.publish-plugin'
}

if (!project.hasProperty('ossrhUsername'))      ext.ossrhUsername       = System.getenv('SONATYPE_USERNAME') ?: '**UNDEFINED**'
if (!project.hasProperty('ossrhPassword'))      ext.ossrhPassword       = System.getenv('SONATYPE_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingKeyId'))       ext.signingKeyId        = System.getenv('SIGNING_KEY_ID') ?: '**UNDEFINED**'
if (!project.hasProperty('signingPassword'))    ext.signingPassword     = System.getenv('SIGNING_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingSecretKey'))   ext.signingSecretKey    = System.getenv('SIGNING_SECRET_KEY_PATH') ? rootProject.file(System.getenv('SIGNING_SECRET_KEY_PATH')).text : '**UNDEFINED**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name = 'Gru'
        vendor = 'Agorapulse'
        description = 'HTTP Testing Framework'

        links {
            website = "https://github.com/${slug}"
            issueTracker = "https://github.com/${slug}/issues"
            scm = "https://github.com/${slug}.git"
        }

        people {
            person {
                id = 'musketyr'
                name = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        enabled = false
        signing {
            enabled =  true
            keyId = signingKeyId
            secretKey = signingSecretKey
            password = signingPassword
        }
        releasesRepository = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }

    quality {
        checkstyle {
            toolVersion = '8.27'
            maxErrors = 12
        }

        codenarc {
            toolVersion = '1.5'
            maxPriority2Violations = 19
            maxPriority3Violations = 91
        }
    }

    coverage {
        coveralls {
            enabled = true
        }
    }

    docs {
        groovydoc {
            aggregate {
                enabled = false
            }
        }
    }

}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo1.maven.org/maven2/' }
        maven { url "https://repo.grails.org/grails/core" }
        maven { url 'https://repo.spring.io/release/' }
    }

    license {
        exclude '**/*.json'
        exclude '**/*.yml'
        exclude '**/*.txt'
        exclude '**/*.html'
        exclude '**/*.dtd'
    }
}


subprojects {
    dependencies {
        codenarc "org.codenarc:CodeNarc:${config.quality.codenarc.toolVersion}"
        codenarc "org.codehaus.groovy:groovy-templates:$groovyVersion"
    }
}

projects {
    subprojects {
        dirs(['libs', 'examples']) { Project subproject ->
            sourceCompatibility = 1.8
            targetCompatibility = 1.8

            repositories {
                mavenCentral()
            }

            tasks.withType(JavaCompile) {
                options.encoding = "UTF-8"
                options.compilerArgs.add('-parameters')
            }

            tasks.withType(GroovyCompile) {
                groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
            }
        }

        dir('libs') {
            processResources {
                filesMatching('**/org.codehaus.groovy.runtime.ExtensionModule') {
                    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: version])
                }
            }

            jar {
                manifest.attributes provider: 'gradle'
            }

            config {
                publishing {
                    enabled = true
                }
            }

            dependencies {
                testImplementation "org.spockframework:spock-core:$spockVersion", {
                    force = true
                }
            }

            tasks.withType(Test) {
                useJUnitPlatform()
            }

            project(':gru-bom').dependencies.constraints.api it
        }

        dir ('examples') {
            groovydoc.onlyIf { false }
            config {
                quality {
                    codenarc {
                        enabled = false
                    }
                }
            }
        }

        dir('platforms') {
            config {
                publishing {
                    enabled = true
                }
            }
        }

        dir('kotlin-libs') {
            repositories {
                mavenCentral()
            }

            jar {
                manifest.attributes provider: 'gradle'
            }

            config {
                publishing {
                    enabled = true
                }
            }

            tasks.withType(Test) {
                useJUnitPlatform()
            }

            project(':gru-bom').dependencies.constraints.api it
        }
    }
}
