/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2023 Agorapulse.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath platform("org.codehaus.groovy:groovy-bom:$groovyVersion")

        classpath group: 'org.kordamp.gradle',                  name: 'checkstyle-gradle-plugin',       version: kordampPluginVersion
        classpath group: 'org.kordamp.gradle',                  name: 'codenarc-gradle-plugin',         version: kordampPluginVersion
        classpath group: 'org.kordamp.gradle',                  name: 'groovy-project-gradle-plugin',   version: kordampPluginVersion
        classpath group: 'org.kordamp.gradle',                  name: 'guide-gradle-plugin',            version: kordampPluginVersion
        classpath group: 'org.kordamp.gradle',                  name: 'settings-gradle-plugin',         version: kordampPluginVersion

        classpath group: 'org.ajoberstar',                      name: 'gradle-git-publish',             version: '2.1.3'
        classpath group: 'io.github.gradle-nexus',              name: 'publish-plugin',                 version: nexusPluginVersion

        classpath group: 'org.jetbrains.kotlin',                name: 'kotlin-gradle-plugin',           version: kotlinVersion
    }
}

plugins {
    id("com.gradle.enterprise") version("3.15.1")
}

apply plugin: 'org.kordamp.gradle.settings'

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

rootProject.name = 'gru-root'

projects {
    cache = false
    directories = ['docs', 'libs', 'examples', 'platforms', 'kotlin-libs']

    plugins {
        dir('docs') {
            id 'org.kordamp.gradle.guide'
            id 'org.ajoberstar.git-publish'
        }
        dirs(['libs', 'examples']) {
            id 'java-library'
            id 'groovy'
        }
        dir('platforms') {
            id 'java-platform'
        }
        dir('kotlin-libs') {
            id 'org.jetbrains.kotlin.jvm'
        }
    }

}

